// General build settings
// https://docs.gradle.org/current/javadoc/org/gradle/api/Project.html#subprojects(groovy.lang.Closure)

def loadEnvFile() {
    def props = new Properties()
    if (rootProject.file(".env").exists()) {
        rootProject.file(".env").withInputStream { props.load(it) }
    }
    return props
}

plugins {
    alias(libs.plugins.spotless)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    dependencies {
        // General dependencies
        implementation libs.spring.context
        implementation libs.slf4j
        implementation libs.logback

        // Use JUnit Jupiter for testing.
        testImplementation libs.junit.jupiter
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    repositories {
        // Use Maven Central for resolving dependencies.
        mavenCentral()
    }

    // Apply a specific Java toolchain to ease working on different environments.
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.named('test') {
        // Use JUnit Platform for unit tests.
        useJUnitPlatform()
    }
    
    spotless {
        java {
            // Форматирование для Java файлов
            googleJavaFormat().aosp()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
} 